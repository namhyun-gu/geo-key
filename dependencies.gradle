ext.versions = [
        compileSdkVersion: 30,
        minSdkVersion    : 23,
        targetSdkVersion : 30,
        versionCode      : getVersionCode(),
        versionName      : "git describe --tags --abbrev=0".execute([], project.rootDir).text.trim()
]

ext.params = [
        NCP_CLIENT_ID        : getKey("NCP_CLIENT_ID"),
        NCP_CLIENT_SECRET    : getKey("NCP_CLIENT_SECRET"),
        KEYSTORE_PASSWORD    : getKey("KEYSTORE_PASSWORD"),
        KEYSTORE_KEY_ALIAS   : getKey("KEYSTORE_KEY_ALIAS"),
        KEYSTORE_KEY_PASSWORD: getKey("KEYSTORE_KEY_PASSWORD")
]

ext.library = [
        kotlin               : "1.4.10",
        kotlinx_serialization: "1.0.0-RC",
        gradle_plugin        : "4.0.1",
        // Jetpack
        dagger               : "2.28.3",
        hilt                 : "1.0.0-SNAPSHOT",
        hilt_android         : "2.28-alpha",
        navigation           : "2.3.1"
]

def getKey(propName) {
    def file = rootProject.file("key.properties")
    if (file.exists()) {
        def properties = new Properties()
        properties.load(new FileInputStream(file))
        if (properties[propName] != null) {
            return "${properties[propName]}"
        }
    }
    return System.getenv(propName)
}

def getVersionCode() {
    if (System.getenv("CI") == "true") {
        return System.getenv("GITHUB_RUN_NUMBER").toInteger()
    } else {
        return Integer.parseInt("git rev-list --count HEAD".execute([], project.rootDir).text.trim())
    }
}